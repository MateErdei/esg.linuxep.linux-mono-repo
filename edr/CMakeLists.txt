cmake_minimum_required(VERSION 3.10)
project(sspl_edr_plugin)

set(SANITISE_FLAGS   "-fsanitize=address -fsanitize=leak -fsanitize=undefined -fsanitize=shift-exponent -fsanitize=unreachable -fsanitize=vla-bound -fsanitize=null -fsanitize=return -fsanitize=signed-integer-overflow -fsanitize=bounds -fsanitize=alignment  -fsanitize=object-size -fsanitize=float-divide-by-zero -fsanitize=float-cast-overflow -fsanitize=returns-nonnull-attribute -fsanitize=nonnull-attribute -fsanitize=bool -fsanitize=enum -fsanitize=vptr -fsanitize=signed-integer-overflow -fsanitize-address-use-after-scope ")
set(WARNING_FLAGS "-Wall -Wextra -Werror -fstack-protector-all")

set(CMAKE_SHARED_LINKER_FLAGS "-Wl,-z,relro -Wl,-z,now")

set(CMAKE_CXX_FLAGS_DEBUG  "${CMAKE_CXX_FLAGS_DEBUG} ${SANITISE_FLAGS} ${WARNING_FLAGS}")
set(CMAKE_CXX_FLAGS  "${CMAKE_CXX_FLAGS} ${WARNING_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS_DEBUG "${SANITISE_FLAGS} ${CMAKE_EXE_LINKER_FLAGS_DEBUG}")

set(CMAKE_POSITION_INDEPENDENT_CODE ON)

if (NOT NO_GCOV STREQUAL true AND CMAKE_BUILD_TYPE STREQUAL Debug)
    set(CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG} -lgcov")
    set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} --coverage")
endif()

set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/buildconfig")
include(SophosAddTest)
include(SophosAddLibrary)

if(NOT GCC_INPUT)
    set (GCC_INPUT "/build/input/gcc")
endif(NOT GCC_INPUT)

if(NOT DEFINED PLUGIN_NAME)
    # developers may just add a file called PLUGINNAME to get all the variables
    # set as the ./build.sh set them out.
    if ( EXISTS "${CMAKE_SOURCE_DIR}/PLUGINNAME" )
        file(READ "${CMAKE_SOURCE_DIR}/PLUGINNAME" PLUGIN_NAME_FROMFILE)
        string( STRIP ${PLUGIN_NAME_FROMFILE} PLUGIN_NAME)
    else()
    set(PLUGIN_NAME edr)
    endif()
endif()
string( TOLOWER ${PLUGIN_NAME} plugin_name)

if(NOT DEFINED PRODUCT_LINE_ID)
    set(PRODUCT_LINE_ID ServerProtectionLinux-Plugin-${PLUGIN_NAME})
endif()

set(COMPONENT_SUITE_LINE_ID ServerProtectionLinux-Plugin-EDR)

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release)
endif(NOT CMAKE_BUILD_TYPE)

if(NOT DEFINED PRODUCT_NAME)
    set(PRODUCT_NAME ${PLUGIN_NAME})
endif()

if(NOT DEFINED DEFAULT_HOME_FOLDER)
    set(DEFAULT_HOME_FOLDER ${PRODUCT_NAME})
endif()

#Specify the feature list of the form "feature1, feature2, feature3"
#Set "" for no features when including in a component suite
set(FEATURE_LIST "EDR")


set(CMAKE_CXX_STANDARD 17)

# Check process children and give detail for each leak
## --trace-children=yes
set(MEMORYCHECK_COMMAND_OPTIONS
        "${MEMORYCHECK_COMMAND_OPTIONS} --leak-check=full --error-exitcode=80")

set(MEMORYCHECK_COMMAND_OPTIONS
        "${MEMORYCHECK_COMMAND_OPTIONS} --gen-suppressions=all --suppressions=${CMAKE_SOURCE_DIR}/build/valgrind/suppressions.supp")


include (CTest)
enable_testing()

set(REL_DIST sdds)
set(DIST "${CMAKE_BINARY_DIR}/${REL_DIST}")
set(CMAKE_INSTALL_PREFIX "${REL_DIST}")

if(NOT DEFINED INPUT)
    set( INPUT ${CMAKE_SOURCE_DIR}/redist )
endif()

if(NOT EXISTS ${INPUT})
    string( APPEND ERRORMESSAGE
            "To build using CLion you first need to:\n"
            "1. Fetch inputs in source folder by running:\n"
            "    python3 -m build_scripts.artisan_fetch build-files/release-package.xml\n"
            "2. Run the full build script with no build flag:\n"
            "    ./build.sh --no-build\n"
            "After that you will be able to build using CLion")
    message( FATAL_ERROR ${ERRORMESSAGE})
endif()

if(NOT DEFINED PLUGINAPIPATH)
    SET( PLUGINAPIPATH ${INPUT}/pluginapi)
endif()

if (NOT DEFINED OSQUERYSDK)
    SET( OSQUERYSDK ${INPUT}/osquerysdk)
endif()

set( BOOST_INPUT ${INPUT}/boost )
set( BOOST_INCLUDE_DIR ${BOOST_INPUT}/include )

set(pluginapiinclude ${PLUGINAPIPATH}/include)
set(pluginapilib ${PLUGINAPIPATH}/lib64/libpluginapi.so)
set(testhelpersinclude ${PLUGINAPIPATH}/tests/include)
set(testhelperslib ${PLUGINAPIPATH}/tests/lib64/libsharedtesthelpers.so)

set(DISTRIBUTION_TOOL_DIR ${PLUGINAPIPATH}/distribution)

file(GLOB zmqlib ${PLUGINAPIPATH}/lib64/libzmq.so.*)
file(GLOB log4cpluslib ${PLUGINAPIPATH}/lib64/liblog4cplus-*.so.*)
file(GLOB protobuflib ${PLUGINAPIPATH}/lib64/libprotobuf.so.*)
file(GLOB expatlib    ${PLUGINAPIPATH}/lib64/libexpat.so.*)
file(GLOB libz    ${PLUGINAPIPATH}/lib64/libz.so.*)


find_library(OSQUERY_LIBRARY
        NAMES osquerysdk
        PATHS ${OSQUERYSDK}/lib64
        NO_DEFAULT_PATH
        )

find_library(GLOG_LIBRARY
        NAMES glog
        PATHS ${OSQUERYSDK}/lib64
        NO_DEFAULT_PATH
        )

find_library(THRIFT_LIBRARY
        NAMES thrift
        PATHS ${OSQUERYSDK}/lib64
        NO_DEFAULT_PATH
        )

find_library(GFLAG_LIBRARY
        NAMES gflags
        PATHS ${OSQUERYSDK}/lib64
        NO_DEFAULT_PATH
        )
find_library(BOOST_CHRONO_LIBRARY
        NAMES boost_chrono
        PATHS ${INPUT}/boost/lib64
        NO_DEFAULT_PATH
        )

find_library(BOOST_SYSTEM_LIBRARY
        NAMES boost_system
        PATHS ${INPUT}/boost/lib64
        NO_DEFAULT_PATH
        )

find_library(BOOST_THREAD_LIBRARY
        NAMES boost_thread
        PATHS ${INPUT}/boost/lib64
        NO_DEFAULT_PATH
        )

find_library(BOOST_FILESYSTEM_LIBRARY
        NAMES boost_filesystem
        PATHS ${INPUT}/boost/lib64
        NO_DEFAULT_PATH
        )

find_library(BOOST_CONTEXT_LIBRARY
        NAMES boost_context
        PATHS ${INPUT}/boost/lib64
        NO_DEFAULT_PATH
        )


add_subdirectory(modules)
add_subdirectory(products)
add_subdirectory(tests)
